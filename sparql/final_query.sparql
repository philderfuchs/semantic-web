PREFIX geo: <http://www.geonames.org/ontology#>
PREFIX pa: <http://www.imn.htwk-leipzig.de/~panders/>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX time: <http://www.w3.org/2006/time/>
PREFIX yago: <http://yago-knowledge.org/resource/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

SELECT ?n ((?countOfFFVenues / ?pop) * 100000 as ?FastFoodDensity) (?sR as ?SuicideRate) ((?cdAverage + ?ucAverage) as ?IbdRate)
WHERE {
  ?c geo:population ?pop.
  ?c geo:officialName ?n.
  ?s a pa:ScdStat.
  ?s dbo:country ?c.
  ?s pa:incidence ?sR.
  {
    SELECT ?c (count(?s) as ?countOfFFVenues) ?cdAverage ?ucAverage
    WHERE {
      ?s a yago:wikicat_Fast-food_restaurants.
      ?s dbo:country ?c.
      {
        SELECT ?c (AVG(?cdR) as ?cdAverage) (AVG(?ucR) as ?ucAverage)
        WHERE {
         ?i a pa:CdStat.
         ?i dbo:country ?c.
         ?i pa:samplingTime ?cdSt.
         ?cdSt time:hasEnd ?cdLatest.
         ?i pa:incidence ?cdR.

         ?j a pa:UcStat.
         ?j dbo:country ?c.
         ?j pa:samplingTime ?ucSt.
         ?ucSt time:hasEnd ?ucLatest. 
         ?j pa:incidence ?ucR. 
            {
              SELECT ?c (MAX(?cdEnd) as ?cdLatest) (MAX(?ucEnd) as ?ucLatest)
              WHERE {
               ?i a pa:CdStat.
               ?i dbo:country ?c.
               ?i pa:samplingTime ?cdSt.
               ?cdSt time:hasEnd ?cdEnd.

               ?j a pa:UcStat.
               ?j dbo:country ?c.
               ?j pa:samplingTime ?ucSt.
               ?ucSt time:hasEnd ?ucEnd.
              }
              GROUP BY ?c
            }
          }
        GROUP BY ?c
      }
    }
    GROUP BY ?c ?cdAverage ?ucAverage
  }
}
ORDER BY DESC(?IbdRate)